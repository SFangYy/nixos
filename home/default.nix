{
  pkgs,
  lib,
  user,
  config,
  ...
}:
{
  imports = [
    ./lib
    ./programs
    ./tweaks
  ];

  home = {
    username = user;
    homeDirectory = "/home/${user}";

    packages = with pkgs; [
      # vcs-env

      # files
      zip
      xz
      unzip

      # utils
      ripgrep
      zoxide
      fzf
      eza
      fd
    ];

    sessionVariables = {
      all_proxy = "http://127.0.0.1:7890";
      ALL_PROXY = "http://127.0.0.1:7890";
      HTTP_PROXY = "http://127.0.0.1:7890";
      HTTPS_PROXY = "http://127.0.0.1:7890";
    };

    activation = {
      niri-transition =
        lib.hm.dag.entryAfter [ "writeBoundary" ]
          # bash
          ''
            run ${pkgs.niri-unstable}/bin/niri msg action do-screen-transition
          '';
      reload-shell =
        lib.hm.dag.entryAfter [ "niri-transition" ]
          # bash
          ''
            # only run stop if the service is active
            if ${pkgs.systemd}/bin/systemctl --user is-active waybar.service; then
              run --silence ${pkgs.systemd}/bin/systemctl --user stop waybar.service
            fi
            if ${pkgs.systemd}/bin/systemctl --user is-active dms.service; then
              run --silence ${pkgs.systemd}/bin/systemctl --user stop dms.service
            fi
            if ${pkgs.systemd}/bin/systemctl --user is-active caelestia.service; then
              run --silence ${pkgs.systemd}/bin/systemctl --user stop caelestia.service
            fi
            if ${pkgs.systemd}/bin/systemctl --user is-active noctalia-shell.service; then
              run --silence ${pkgs.systemd}/bin/systemctl --user stop noctalia-shell.service
            fi
            run --silence ${pkgs.systemd}/bin/systemctl --user start ${config.desktopShell}.service
          '';
    };
  };

  i18n.inputMethod = {
    enable = true;
    type = "fcitx5";
    fcitx5 = {
      addons = with pkgs; [
        fcitx5-gtk
        libsForQt5.fcitx5-qt
        qt6Packages.fcitx5-chinese-addons
        fcitx5-rime
        fcitx5-pinyin-moegirl
        fcitx5-pinyin-zhwiki
      ];
      waylandFrontend = true;
    };
  };

  programs = {
    git = {
      enable = true;
      settings = {
        user = {
          name = "SFangYy";
          email = "sfangyy@163.com";
        };
        safe = {
          directory = "*";
        };
        http.proxy = "http://127.0.0.1:7890";
      };
    };

    nix-index = {
      enable = true;
      enableFishIntegration = true;
      enableBashIntegration = false;
      enableZshIntegration = false;
    };

    home-manager.enable = true;
  };

  nix.package = pkgs.nix;
}
